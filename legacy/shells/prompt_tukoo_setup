# Theme tukoo

prompt_tukoo_precmd() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_tukoo_colors[1]}%b%f%u%c${_prompt_tukoo_colors[4]}●%f)"
  else
    branch_format="(${_prompt_tukoo_colors[1]}%b%f%u%c)"
  fi

  # Define prompts.
  #RPROMPT='${editor_info[overwrite]}%(?:: %F{1}⏎%f)${VIM:+" %B%F{6}V%f%b"}'

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi

  # battery
  if [[ `uname` == 'Darwin' ]]; then
    battery_info=`~/.dotfiles/bin/batcharge.py`
  else
    battery_info=''
  fi

}

prompt_online() {
  return
  # Network Status
  ONLINE='%{%F{green}%}◉'
  OFFLINE='%{%F{red}%}⦿'

  if [[ -f ~/.offline ]]; then
    echo $OFFLINE
  else
    echo $ONLINE
  fi
}

prompt_battery() {
  echo `~/.dotfiles/bin/batcharge.py`
}

integer PSCOL=1

prompt_tukoo_setup() {
  setopt LOCAL_OPTIONS
  setopt ZLE
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_tukoo_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_tukoo_colors=(
    "%F{81}"  # Turquoise
    "%F{166}" # Orange
    "%F{135}" # Purple
    "%F{161}" # Hotpink
    "%F{118}" # Limegreen
    )
  else
    _prompt_tukoo_colors=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_tukoo_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_tukoo_colors[5]}%a%f)"
  local unstaged_format="${_prompt_tukoo_colors[2]}●%f"
  local staged_format="${_prompt_tukoo_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  local -h vi_indicator="<not vi mode>"
  vicmd_ind=' %{%B%F{2}%}<%{%F{3}%}<%{%F{1}%}<%{%f%b%} '
  viins_ind=' %{%B%F{1}%}>%{%F{3}%}>%{%F{2}%}>%{%f%b%} '

  # Set editor-info parameters.
  #  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  #  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  #  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  #  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  local -ah ps1
  local -h date time rs="%b%f%k" fmt

  # Set up the components of the upper line
  date="%{%F{1}%}%D$rs" # Prefer "%{$fg[%D]%}%W$rs" in the USA?
  time="%{%F{2}%}%*$rs"

  # Assemble the new prompt
  ps1=(
  '%$[COLUMNS-PSCOL-19]>..>'    # Begin truncation (upper left prompt)
  "${_prompt_tukoo_colors[3]}%n%f"
  " at ${_prompt_tukoo_colors[2]}%m%f"
  " in ${_prompt_tukoo_colors[5]}%~%f"
  ' ${vcs_info_msg_0_}'
  '${(l:$COLUMNS::.:: :)}'      # Fill in the space
  "%<<"                         # End truncation (end upper left prompt)
  '%$[COLUMNS-PSCOL-15](l. . ' "$date)"
  '%$[COLUMNS-PSCOL-7](l.. ' "$time)"
  $'\n'
  '$python_info[virtualenv]'
  '${vi_ind}'
  )
  # Define prompts.
  setopt promptsubst
  PROMPT="${(j::)ps1}"
  RPROMPT='[ $? ] $(prompt_online) ${battery_info}'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

prompt_tukoo_setup "$@"
